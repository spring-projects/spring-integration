apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				afterEvaluate {
					name = project.description
					description = project.description
				}
				url = linkScmUrl
				organization {
					name = 'Spring IO'
					url = 'https://spring.io/projects/spring-integration'
				}
				licenses {
					license {
						name = 'Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}
				scm {
					url = linkScmUrl
					connection = linkScmConnection
					developerConnection = linkScmDevConnection
				}
				developers {
					developer {
						id = 'abilan'
						name = 'Artem Bilan'
						email = 'abilan@pivotal.io'
						roles = ["project lead"]
					}
					developer {
						id = 'garyrussell'
						name = 'Gary Russell'
						email = 'grussell@pivotal.io'
						roles = ["project lead emeritus"]
					}
					developer {
						id = 'markfisher'
						name = 'Mark Fisher'
						email = 'mfisher@pivotal.io'
						roles = ["project founder and lead emeritus"]
					}
				}
				issueManagement {
					system = 'GitHub'
					url = linkIssue
				}
			}
			versionMapping {
				usage('java-api') {
					fromResolutionResult()
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}

artifactoryPublish {
	publications(publishing.publications.mavenJava)
}
